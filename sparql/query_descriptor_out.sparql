PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX meshx: <http://mesh.medvik.cz/link/>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>



## Descriptor query v1.0
## endpoint: http://localhost:3030/mesh/query

##CONSTRUCT { mesh:D005123 ?p ?o . } {
SELECT DISTINCT * {
{SELECT ?p ?o
WHERE {
  mesh:D005123 ?p ?o
  FILTER(isLiteral(?o))
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:scopeNote as ?p)
    mesh:D005123 meshv:preferredConcept ?concept .
    ?concept meshv:scopeNote ?o
}} UNION {
SELECT ?p ?o
WHERE {
   BIND(rdf:type as ?p)
   mesh:D005123 ?p ?o
}} UNION {
SELECT ?p ?o
WHERE {
  mesh:D005123 ?p ?id .
  ?id rdfs:label ?val
  FILTER(!isLiteral(?id))
  BIND(STRLANG(CONCAT(STR(?id), '|', STR(?val)), LANG(?val)) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:narrowerDescriptor as ?p)
    ?id ?px mesh:D005123 .
    ?id rdfs:label ?val
    FILTER(?px IN(meshv:broaderDescriptor))
    FILTER(!isLiteral(?id))
    BIND(STRLANG(CONCAT(STR(?id), '|', STR(?val)), LANG(?val)) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:preferredTerm as ?p)
    mesh:D005123 meshv:preferredConcept ?c .
    ?c ?p ?id .
    ?id meshv:prefLabel ?val
    BIND(STRLANG(CONCAT(STR(?id), '|', STR(?val)), LANG(?val)) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(mesht:preferredTerm  as ?p)
    mesh:D005123 meshv:preferredConcept ?c .
    ?c ?p ?id .
    ?id mesht:prefLabel ?val
    BIND(STRLANG(CONCAT(STR(?id), '|', STR(?val)), LANG(?val)) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:seeAlso as ?p)
    ?id ?px mesh:D005123 .
    ?id rdfs:label ?val
    FILTER(?px IN(meshv:seeAlso))
    FILTER(!isLiteral(?id))
    BIND(STRLANG(CONCAT(STR(?id), '|', STR(?val)), LANG(?val)) AS ?o)
}}

} ORDER BY ?p
