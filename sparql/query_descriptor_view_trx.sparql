PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX meshv:  <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh:   <http://id.nlm.nih.gov/mesh/>
PREFIX text:   <http://jena.apache.org/text#>
PREFIX mesht:  <http://www.medvik.cz/schema/mesh/vocab/#>
PREFIX meshx: <http://mesh.medvik.cz/link/>

SELECT DISTINCT * {
{SELECT ?p ?o
WHERE {
  mesh:D009369 ?p ?val
  FILTER(isLiteral(?val))
  BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
   BIND(rdf:type as ?p)
   mesh:D009369 ?p ?val
   BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:scopeNote as ?p)
    mesh:D009369 meshv:preferredConcept ?concept .
    ?concept ?p ?val
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(mesht:scopeNote as ?p)
    mesh:D009369 meshv:preferredConcept ?concept .
    ?concept ?p ?val
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
  mesh:D009369 ?p ?id .
  ?id rdfs:label ?val
  FILTER(!isLiteral(?id))
  FILTER(?p not in (meshv:allowableQualifier,meshv:treeNumber))
  BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:narrowerDescriptor as ?p)
    ?id ?px mesh:D009369 .
    ?id rdfs:label ?val
    FILTER(?px IN(meshv:broaderDescriptor))
    FILTER(!isLiteral(?id))
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:term as ?p)
    mesh:D009369 meshv:preferredConcept ?c .
    ?c ?p ?id .
    ?id meshv:prefLabel ?val
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(mesht:term as ?p)
    mesh:D009369 meshv:preferredConcept ?c .
    ?c ?p ?id .
    ?id mesht:prefLabel ?val
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(mesht:preferredConcept as ?p)
    mesh:D009369 meshv:preferredConcept ?c .
    ?c mesht:preferredTerm ?id .
    ?id mesht:prefLabel ?val
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(meshv:seeAlso as ?p)
    ?id ?px mesh:D009369 .
    ?id rdfs:label ?val
    FILTER(?px IN(meshv:seeAlso))
    FILTER(!isLiteral(?id))
    BIND(STR(?val) AS ?o)
}} UNION {
SELECT ?p ?o
WHERE {
    BIND(mesht:concept as ?p)
    mesh:D009369 meshv:concept ?c .
    ?c mesht:preferredTerm ?id .
    ?id mesht:prefLabel ?val
    BIND(STR(?val) AS ?o)
}}

} ORDER BY ?p ?o


